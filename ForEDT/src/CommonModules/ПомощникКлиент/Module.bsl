Процедура ПриНачалеРаботыСистемы() Экспорт
	
	Если НЕ СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ОбработкаДействияСообщения", ЭтотОбъект);
	СистемаВзаимодействия.ПодключитьОбработчикДействияСообщения(Обработчик);

	ОбработчикФормированияКоманд = Новый ОписаниеОповещения("ОбработкаФормированияКоманд", ЭтотОбъект);
	СистемаВзаимодействия.ПодключитьОбработчикФормированияКоманд(ОбработчикФормированияКоманд);
	
КонецПроцедуры

Процедура ОбработкаДействияСообщения(Сообщение, Действие, ДопПараметры) Экспорт
	
	Если Действие = "Settings" Тогда
		ОткрытьФорму("ОбщаяФорма.НастройкаПомощникаНеотработанныхЗаказов");
		Возврат;
	КонецЕсли;
	
	Если Действие = "CheckNow" Тогда
		Помощник.ПомощникНеотработанныеЗаказы();
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаФормированияКоманд(Параметры, Команды, КомандаПоУмолчанию, ДопПараметры) Экспорт
	
	Если НЕ СистемаВзаимодействия.ПоддерживаетсяРаботаСВложениями() Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Источник = ИсточникКомандСистемыВзаимодействия.Вложение Тогда
		
		Команды.Добавить(Новый ОписаниеКомандыСистемыВзаимодействия(Неопределено));
		
		ДопПараметры = Новый Структура();
		ДопПараметры.Вставить("Вложение", Параметры.Вложение);
		ДопПараметры.Вставить("Обсуждение", Параметры.Обсуждение);
		ДопПараметры.Вставить("УстановитьКартинку", Истина);
		
		Команда = Новый ОписаниеКомандыСистемыВзаимодействия(
			Новый ОписаниеОповещения("ДобавитьФайлККарточкеТовара", ЭтотОбъект, ДопПараметры),
			НСтр("ru = 'Установить как картинку товара'", "ru"));
		Команда.Доступность = СтрНачинаетсяС(Параметры.Вложение.ТипСодержимого, "image/");
		Команды.Добавить(Команда);

		
		ДопПараметры = Новый Структура();
		ДопПараметры.Вставить("Вложение", Параметры.Вложение);
		ДопПараметры.Вставить("Обсуждение", Параметры.Обсуждение);
		ДопПараметры.Вставить("УстановитьКартинку", Ложь);
		
		Команда = Новый ОписаниеКомандыСистемыВзаимодействия(
			Новый ОписаниеОповещения("ДобавитьФайлККарточкеТовара", ЭтотОбъект, ДопПараметры),
			НСтр("ru = 'Добавить файл в карточку товара'", "ru"));
		Команды.Добавить(Команда);
		
	КонецЕсли;

КонецПроцедуры

Процедура ПоместитьФайлВКарточкуТовара(Вложение, УстановитьКартинку, Форма = Неопределено, Ссылка = Неопределено)
	
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("Имя", Вложение.Наименование);
	ДопПараметры.Вставить("УстановитьКартинку", УстановитьКартинку);
	ДопПараметры.Вставить("Форма", Форма);
	ДопПараметры.Вставить("Ссылка", Ссылка);

	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОткрытияПотокаДляЧтения", ЭтотОбъект, ДопПараметры);
	Вложение.НачатьОткрытиеПотокаДляЧтения(ОписаниеОповещения);

КонецПроцедуры

Процедура ПослеОткрытияПотокаДляЧтения(Поток, ДопПараметры) Экспорт
	
	ЧтениеДанных = Новый ЧтениеДанных(Поток);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЧтенияВБуферДвоичныхДанных", ЭтотОбъект, ДопПараметры);
	ЧтениеДанных.НачатьЧтениеВБуферДвоичныхДанных(ОписаниеОповещения);

КонецПроцедуры

Процедура ПослеЧтенияВБуферДвоичныхДанных(Буфер, ДопПараметры) Экспорт
	
	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(Буфер);
	
	Форма = ДопПараметры.Форма;
	Если Форма <> Неопределено Тогда
		
		Форма.ДобавитьФайл(ДвоичныеДанные, ДопПараметры.Имя, ДопПараметры.УстановитьКартинку);
		
	Иначе
		Ссылка = ДопПараметры.Ссылка;
		Если Ссылка <> Неопределено Тогда
			
			Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
			Помощник.ДобавитьФайлКТовару(Ссылка, Адрес, ДопПараметры.Имя, ДопПараметры.УстановитьКартинку);
			ОповеститьОбИзменении(Тип("СправочникСсылка.ХранимыеФайлы"));
			
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Функция НайтиОткрытуюФорму(НавигационнаяСсылка)
	
	Окна = ПолучитьОкна();
	Для Каждого Окно Из Окна Цикл
		
		Если Окно.Основное ИЛИ Окно.НачальнаяСтраница Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если Окно.ПолучитьНавигационнуюСсылку() = НавигационнаяСсылка Тогда
			
			Содержимое = Окно.Содержимое;
			Если Содержимое <> Неопределено И Содержимое.Количество() > 0 Тогда
				
				Возврат Содержимое[0];
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ДобавитьФайлККарточкеТовара(ДопПараметры) Экспорт

	Если ДопПараметры.Обсуждение.КонтекстОбсуждения <> Неопределено Тогда
		
		НавигационнаяСсылка = ДопПараметры.Обсуждение.КонтекстОбсуждения.НавигационнаяСсылка;
		Если СтрНачинаетсяС(НавигационнаяСсылка, "e1cib/data/Справочник.Товары?ref=") Тогда
			
			Форма = НайтиОткрытуюФорму(НавигационнаяСсылка);
			Если Форма <> Неопределено Тогда
			
				ПоместитьФайлВКарточкуТовара(ДопПараметры.Вложение, ДопПараметры.УстановитьКартинку, Форма);
				Возврат;
				
			КонецЕсли;
						
		КонецЕсли;
		
	Иначе
		
		Параметры = Новый Структура();
		Параметры.Вставить("РежимВыбора", Истина);
		ФормаВыбора = ПолучитьФорму("Справочник.Товары.ФормаВыбора", Параметры);
		
		ПараметрыОписанияОповещения = Новый Структура();
		ПараметрыОписанияОповещения.Вставить("Вложение", ДопПараметры.Вложение);
		ПараметрыОписанияОповещения.Вставить("УстановитьКартинку", ДопПараметры.УстановитьКартинку);
		ФормаВыбора.ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ПослеЗакрытияФормыВыбора", ЭтотОбъект, ПараметрыОписанияОповещения);
		
		ФормаВыбора.Открыть();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗакрытияФормыВыбора(РезультатЗакрытия, ДопПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		
		Форма = Неопределено;
		
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(РезультатЗакрытия);
		Если НавигационнаяСсылка <> "" Тогда
			
			Форма = НайтиОткрытуюФорму(НавигационнаяСсылка);
			
		КонецЕсли;
		
		ПоместитьФайлВКарточкуТовара(ДопПараметры.Вложение, ДопПараметры.УстановитьКартинку, Форма, РезультатЗакрытия);

	КонецЕсли;
	
КонецПроцедуры
